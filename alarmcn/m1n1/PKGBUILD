# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.4.20
pkgrel=1
pkgdesc='Asahi Linux bootloader'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(git imagemagick)
source=(
  "git+https://github.com/AsahiLinux/m1n1#tag=v${pkgver}"
)
sha256sums=('4be293c78f39f804ee977f8c528b1807549929762d45f91c46ab280e5a8b82cd')
b2sums=('6facbb2501d8fb8f7a0773213b37fca7f7183b36c361b373d7b6959982138082ef65c72c4ae6c277a33214c16dbd165d2eb252c92b62492a4731cabe2c6e766d')

prepare() {
  cd m1n1
  git submodule update --init --recursive
}

build() {
  cd m1n1
  make ARCH= RELEASE=1 -j1
}

package() {
  install -Dt "$pkgdir/usr/lib/asahi-boot" -m644 "m1n1/build/m1n1.bin"
  install -Dm644 m1n1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
