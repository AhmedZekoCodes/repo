# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.4.18
pkgrel=1
pkgdesc='Asahi Linux bootloader'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(git imagemagick)
source=(
  "git+https://github.com/AsahiLinux/m1n1#tag=v${pkgver}"
)
sha256sums=('049466f4b088871128bd7b4b1dc8fe52d6da72b6300cfc597a6e314c0451a01f')
b2sums=('fae42c3e6962f29b00418bc4280c049154cfecb063e350fe651a4d120f7d0d88de824003bd9ab0f5381f1b081cb7241242045746badab365f9b31f0ba81cb809')

prepare() {
  cd m1n1
  git submodule update --init --recursive
}

build() {
  cd m1n1
  make ARCH= RELEASE=1 -j1
}

package() {
  install -Dt "$pkgdir/usr/lib/asahi-boot" -m644 "m1n1/build/m1n1.bin"
  install -Dm644 m1n1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
