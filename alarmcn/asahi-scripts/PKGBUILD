# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=asahi-scripts
pkgver=20250128
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
        update-m1n1)
install=asahi-scripts.install
sha256sums=('f3f1a418b6cccbab4d34c60ffa3aeac6dcb5f2111a1ba4bcdcab0a7abcec78fa'
            '7ef2863184996a52cb2a2f8ce4df6331916a3aefb3314d9aeae92f84c7b4bac0')
b2sums=('c3cf9a4fb275dfa7970b36c73aa87d960ef748f2c442f95a30ed1d0d4dab998887f250f3e74595316b5d4ed72229e5aae0606d3d04035350110fe50c8df07940'
        'b8417590b7fcb827cbb48201b27f124dd9edda5852f550de8884c85578a948441aace3e0d9828417ed9363491d388ab65ae94fec149526b826af013569518157')
backup=(etc/m1n1.conf
        etc/defaults/update-m1n1)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install-arch

  install -Dm644 "$srcdir/update-m1n1" "$pkgdir/etc/defaults/update-m1n1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
