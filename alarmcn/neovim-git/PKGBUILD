# Maintainer: Florian Walch <florian+aur@fwalch.com>
# Contributor: Florian Hahn <flo@fhahn.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=neovim-git
_pkgname=${pkgname%-git}
pkgver=0.10.0.r885.ga6c4487e8b
pkgrel=1
pkgdesc="Fork of Vim aiming to improve user experience, plugins, and GUIs."
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://${_pkgname}.io"
backup=('etc/xdg/nvim/sysinit.vim')
license=('Apache-2.0 AND LicenseRef-vim')
depends=('libluv' 'libtermkey' 'libutf8proc' 'libuv' 'libvterm>=0.3' 'luajit' 'msgpack-c' 'unibilium' 'tree-sitter-git' 'lua51-lpeg' 'libutf8proc-git')
makedepends=('cmake' 'git' 'gperf' 'lua51-mpack')
optdepends=('python2-neovim: for Python 2 plugin support (see :help provider-python)'
            'python-neovim: for Python 3 plugin support (see :help provider-python)'
            'ruby-neovim: for Ruby plugin support (see :help provider-ruby)'
            'xclip: for clipboard support (or xsel) (see :help provider-clipboard)'
            'xsel: for clipboard support (or xclip) (see :help provider-clipboard)'
            'wl-clipboard: for clipboard support on wayland (see :help clipboard)')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("https://github.com/${_pkgname}/${_pkgname}.git")
install="${pkgname}.install"
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}/"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -S "${pkgname}/" \
        -B build \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D USE_BUNDLED=OFF

  cmake --build build
}

check() {
  cd build
  ./bin/nvim --version
  ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install

  cd "${_pkgname}/"
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
  install -Dm644 runtime/nvim.desktop -t "${pkgdir}/usr/share/applications/"
  install -Dm644 runtime/nvim.png -t "${pkgdir}/usr/share/pixmaps/"

  # Make Arch vim packages work
  install -d "${pkgdir}/etc/xdg/nvim/"
  echo "\" This line makes pacman-installed global Arch Linux vim packages work." > "${pkgdir}"/etc/xdg/nvim/sysinit.vim
  echo "source /usr/share/nvim/archlinux.vim" >> "${pkgdir}/etc/xdg/nvim/sysinit.vim"

  install -d "${pkgdir}/usr/share/vim/"
  echo "set runtimepath+=/usr/share/vim/vimfiles" > "${pkgdir}/usr/share/nvim/archlinux.vim"
}
