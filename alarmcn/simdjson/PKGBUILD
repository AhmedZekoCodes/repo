# Maintainer: Bert Peters <bert@bertptrs.nl>
pkgname=simdjson
epoch=1
pkgver=3.12.3
pkgrel=1
pkgdesc="A C++ library to see how fast we can parse JSON with complete validation."
arch=(aarch64 x86_64)
url="https://github.com/simdjson/simdjson"
license=('Apache-2.0')
provides=(libsimdjson.so)
makedepends=(cmake)
source=("$pkgname-$pkgver.tar.gz::https://github.com/simdjson/simdjson/archive/v$pkgver.tar.gz")
sha256sums=('d0af071f2f4187d8b26b556e83ef832b634bd5feb4e2f537b9dabbd334d4e334')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
        -DBUILD_SHARED_LIBS='On' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir/" cmake --install build
}
