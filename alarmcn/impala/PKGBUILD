# Maintainer: orhun <orhunparmaksiz@gmail.com>

pkgname=impala
pkgver=0.2.2
pkgrel=1
pkgdesc="TUI for managing wifi"
url="https://github.com/pythops/${pkgname}"
depends=('gcc-libs' 'iwd')
makedepends=('cargo')
arch=('x86_64' 'aarch64' 'riscv64')
license=('GPL-3.0-or-later')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('ed53562e6f99974dbc635e78a9bf4297b09c5a2aac42811368f02b52cff570939940ba017b3cd2d33e5dba79d7a7be674eb19bc79c8f91eac2d2378ad07d0e37')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --release --frozen
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 Readme.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
