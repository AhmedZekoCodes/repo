# Maintainer: Guillaume Dolle  <dev at gdolle.com>
pkgname=micromamba
pkgver=1.5.9
pkgrel=1
pkgdesc="C++ tiny version of mamba, the fast conda package installer"
arch=('aarch64' 'armv7h' 'i686' 'x86_64')
url="https://github.com/mamba-org/mamba"
license=('MIT')
depends=('libarchive.so' 'libcurl.so' 'libsolv' 'libfmt.so'
         'libspdlog.so' 'libyaml-cpp.so' 'libreproc.so' 'libreproc++.so'
         'python')
makedepends=('gtest' 'termcolor' 'cmake' 'cli11' 'nlohmann-json'
             'ghc-filesystem' 'tl-expected')
source=(micromamba-${pkgver}.tar.gz::https://github.com/mamba-org/mamba/archive/refs/tags/micromamba-${pkgver}.tar.gz
        aea04951d07c00e3665e80278e84eb9e6c9e3666.patch)
sha256sums=('9ac3fb39fffb9a57a7cc102e885cf49d9bac47ec6446c7d8c850f6fc87b26af6'
            '424bd94759673e381008ab77d3de4a7804118b2eb0741fc2650051921c31f126')
provides=('micromamba')
conflict=('micromamba' 'micromamba-bin')

prepare() {
  cd mamba-micromamba-${pkgver}

  patch -Np1 --no-backup-if-mismatch < ../aea04951d07c00e3665e80278e84eb9e6c9e3666.patch
}

build() {
  cd mamba-micromamba-${pkgver}

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
        -DBUILD_LIBMAMBA=ON \
        -DBUILD_LIBMAMBAPY=OFF \
        -DBUILD_LIBMAMBA_TESTS=OFF \
        -DBUILD_MAMBA_PACKAGE=OFF \
        -DBUILD_MICROMAMBA=ON \
        -DBUILD_SHARED=ON \
        -DBUILD_STATIC=OFF \
        -B build
  cmake --build build
}

check() {
  cd mamba-micromamba-${pkgver}/build
  ctest
}

package() {
  cd mamba-micromamba-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
}
