# Maintainer: Zhuoyun Wei <wzyboy@wzyboy.org>

pkgname=beangulp
pkgdesc='Importers framework for Beancount'
pkgver=0.1.1
pkgrel=3
arch=("any")
url="https://github.com/beancount/beangulp"
license=('GPL')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
b2sums=('b5e7015e3a22cd44e5c2b648cf3ba9bfb327f55d43a582d1dae8d7c03d293341f405952ff17b16464cda394ee5acf08aa518b3685e7470c0a630c8accf11ecbc')
depends=("python" "beancount>=3" "python-beautifulsoup4" "python-chardet" "python-click" "python-lxml" "python-magic")
makedepends=("python-build" "python-installer" "python-setuptools" "python-wheel")

build() {
  cd "${pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
