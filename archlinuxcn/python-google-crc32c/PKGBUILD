# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=python-crc32c
pkgname=python-google-crc32c
epoch=1
pkgver=1.7.1
pkgrel=1
pkgdesc='Python wrapping for google-crc32c'
arch=('x86_64')
url='https://github.com/googleapis/python-crc32c'
license=('Apache-2.0')
depends=(
  glibc
  google-crc32c
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/googleapis/python-crc32c/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('00527070a67e844376591dae414fad3193e09d04ad81947e48ba5fcbde5d4c3394ea6e1762d915afca42496d800a1196e938ba356073bca52fd600ef47750d4f')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
