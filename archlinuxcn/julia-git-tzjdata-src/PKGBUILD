pkgname=julia-git-tzjdata-src
pkgver=1.3.1+2024b
_commit=3ef632e06316de4d04a3cfee62e23b2b3b0e2398
pkgrel=1
pkgdesc="TZJData.jl"
url="https://github.com/JuliaTime/TZJData.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts wget)
depends=(julia-git)
source=("git+https://github.com/JuliaTime/TZJData.jl.git#commit=$_commit")
sha256sums=('f3ea19c2c954054e2ef414bad5b42a57e2d1cfb3525f3ebbd142432e3958ab2a')

build() {
  cd TZJData.jl

  rm -rf gen

  local url
  url=$(sed -ne '/url *= *".*"/{s/ *url *= *"\(.*\)" */\1/p;q}' Artifacts.toml)

  wget -O tzjfile-tzdata.tar.gz "$url"

  (
    rm -rf tzjdata/
    mkdir tzjdata/
    cd tzjdata/
    tar xf ../tzjfile-tzdata.tar.gz
    rm ../tzjfile-tzdata.tar.gz
  )

  sed -i -e 's|artifact"tzjdata"|joinpath(@__DIR__, "../tzjdata")|' src/TZJData.jl
}

package() {
  cd TZJData.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh TZJData "${pkgdir}" "${pkgname}" julia-git
}
