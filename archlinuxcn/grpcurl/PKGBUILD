# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Cody Schafer <dev@codyps.com>

pkgname=grpcurl
pkgver=1.9.2
pkgrel=1
pkgdesc="Like cURL, but for gRPC: Command-line tool for interacting with gRPC servers"
arch=('x86_64')
url="https://github.com/fullstorydev/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=("${pkgname}-bin" "${pkgname}-git")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('19916bc36443af60c8d079343ca3aeb1b000afb97e2b13e4beec59ad3dd12356820da456841533e934b4bb78f29235144c54b3cf35372bed73047bef284d3f5f')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p build
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -o build ./cmd/...
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    go test ./...
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm0755 "build/${pkgname}" -t "${pkgdir}/usr/bin/"
}
