# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=SimpleITK
pkgname=(simpleitk python-simpleitk)
pkgver=2.4.0
pkgrel=5
pkgdesc="A layer built on top of the Insight Toolkit (ITK), intended to simplify and facilitate ITK's use in rapid prototyping, education and interpreted languages."
arch=('x86_64')
url='http://www.simpleitk.org'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  itk
)
makedepends=(
  cmake
  doxygen
  eigen
  gcc13
  git
  lua
  python-build
  python-installer
  python-numpy
  python-setuptools
  python-wheel
  swig
)
source=("${_pkgname}::git+https://github.com/SimpleITK/SimpleITK.git#tag=v${pkgver}")
sha512sums=('74d7032e57dcfcbeee270969a86572356f4901b7b71968d082c782e6115ae1806776703b8374251dcda611ec20bcaf6676040815c3d9ae6373b05bb5502c411e')

build() {
  cmake \
    -B ${srcdir}/build \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DBUILD_TESTING:BOOL=OFF \
    -DCMAKE_BUILD_TYPE:STRING=None \
    -DCMAKE_CXX_COMPILER=g++-13 \
    -DCMAKE_C_COMPILER=gcc-13 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSimpleITK_BUILD_DISTRIBUTE:BOOL=ON \
    -DSimpleITK_BUILD_STRIP:BOOL=ON \
    -DSimpleITK_EXPLICIT_INSTANTIATION:BOOL=OFF \
    -DSimpleITK_PYTHON_USE_VIRTUALENV:BOOL=OFF \
    -DWRAP_CSHARP:BOOL=OFF \
    -DWRAP_DEFAULT:BOOL=OFF \
    -DWRAP_JAVA:BOOL=OFF \
    -DWRAP_LUA:BOOL=OFF \
    -DWRAP_PYTHON:BOOL=ON \
    -DWRAP_R:BOOL=OFF \
    -DWRAP_RUBY:BOOL=OFF \
    -DWRAP_TCL:BOOL=OFF \
    -S "${srcdir}/${_pkgname}"
  make -C "${srcdir}/build"
  cd "${srcdir}/build/Wrapping/Python"
  python -m build --wheel --no-isolation
}

package_simpleitk() {
  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install
}

package_python-simpleitk() {
  pkgdesc="${pkgdesc} (Python binding)"
  depends+=(
    python-numpy
    simpleitk
  )
  cd "${srcdir}/build/Wrapping/Python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
