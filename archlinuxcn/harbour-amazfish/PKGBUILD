# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Ikosse <andrelofgren@hotmail.co.uk>

pkgname=harbour-amazfish
pkgver=2.5.0
pkgrel=1
pkgdesc="Companion application for Huami Devices and the Pinetime Infinitime"
arch=('x86_64' 'aarch64')
url="https://github.com/piggz/${pkgname}"
provides=('amazfish')
license=('GPL-3.0-or-later')
depends=(
	'qt5-base'
	'karchive5'
	'kdb'
	'kcontacts5'
	'qt5-location'
	'qt5-connectivity'
	'qtmpris'
	'nemo-qml-plugin-dbus'
	'kirigami2'
	'bluez-qt5'
	'mlite'
	'libpulse'
)
makedepends=('git' 'qt5-tools' 'cmake')
source=(
	"git+${url}.git#tag=${pkgver}"
	"git+https://github.com/piggz/qble.git"
	"git+https://github.com/piggz/libwatchfish.git"
)
sha512sums=('d3b94e27d78b6b54bd7cd9f51a71ae2fd7be6d8669320dc68bb198329b1de73531d2b5313c3b8d6e35b8f13448af8790be9d3a52a20f22b04b6bba2730e6f1bd'
            'SKIP'
            'SKIP')

prepare() {
	cd "${pkgname}/"
	git submodule init
	git config submodule.qble.url "${srcdir}/qble"
	git config submodule.daemon/libwatchfish.url "${srcdir}/libwatchfish"
	git -c protocol.file.allow=always submodule update
}

build() {
	cmake -B build \
		-S "${pkgname}" \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D FLAVOR=silica

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
