# Maintainer : Xeonacid <h.dwwwwww@gmail.com>
# Maintainer : Leon MÃ¶ller <jkhsjdhjs at totally dot rip>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname='perl-compress-raw-lzma'
pkgver='2.212'
pkgrel=3
pkgdesc="Low-Level Perl Interface to lzma compression library"
_dist=Compress-Raw-Lzma
arch=('i686' 'x86_64')
url="https://metacpan.org/release/$_dist"
license=('GPL-1.0-or-later' 'Artistic-1.0-Perl')
options=('!emptydirs' 'purge')
depends=('glibc' 'perl' 'perl-pod-markdown' 'xz')
source=("https://cpan.metacpan.org/authors/id/P/PM/PMQS/$_dist-$pkgver.tar.gz")
md5sums=('dd5e1b2d76ad20c69843d1419fde09ca')
sha512sums=('4b94577a4fb7504698e7e4a01283cace8569221e1873ed8dd7670363530abb2f93bdb611cf17b1baa189fd1d6c1de884a695d1295e4e09be73422ba5753da612')

build() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
