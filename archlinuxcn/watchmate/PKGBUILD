# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Dan Johansen <strit at strits dot dk>
# Contributor: Mark Wagie <mark at manjaro dot org>

pkgname=watchmate
pkgver=0.5.3
pkgrel=1
pkgdesc="PineTime smart watch companion app for Linux phone and desktop"
arch=('x86_64' 'aarch64')
url="https://github.com/azymohliad/${pkgname}"
license=('GPL-3.0-or-later')
depends=('glib2' 'bzip2' 'bluez' 'libadwaita' 'dbus' 'dconf' 'hicolor-icon-theme')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('640d9460a0dcef70fcdd235ba23196d0e0e8b5648040874162458599ae39fd8b')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"

	cd assets
	local _app_id=io.gitlab.azymohliad.WatchMate
	install -Dm644 "${_app_id}.desktop" -t "${pkgdir}/usr/share/applications/"
	install -Dm644 "${_app_id}.gschema.xml" -t "${pkgdir}/usr/share/glib-2.0/schemas/"
	install -Dm644 "${_app_id}.metainfo.xml" -t "${pkgdir}/usr/share/metainfo/"
	install -Dm644 icons/${_app_id}{,-symbolic}.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
