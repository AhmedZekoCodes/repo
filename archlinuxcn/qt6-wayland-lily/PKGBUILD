# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=qt6-wayland
pkgname=qt6-wayland-lily
_pkgver=6.9.0
pkgver=6.9.0
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
provides=(qt6-wayland=$pkgver)
conflicts=(qt6-wayland)
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='Provides APIs for Wayland, patched by lilydjwg'
depends=(gcc-libs
         glibc
         libglvnd
         libxkbcommon
         qt6-base
         qt6-declarative
         qt6-svg
         wayland)
makedepends=(cmake
             git
             ninja
             vulkan-headers)

_pkgfn=qtwayland
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$_pkgver
        0001-text-input-v3-use-dashed-underline-instead-of-solid-.patch
        0002-text-input-v3-always-update-cursor-rectangle-after-e.patch
        0005-fix-egl-compositor-shutdown.patch)
sha256sums=('b5507dcbd923b70e5e4b0b866f422070c51e1fc0a25972ca7af38bf3660870b7'
            aa80925ad53ee9762962bfa6cc9635ee4962c98e707bf7712db88b294cdccde5
            5bb012c561badd9c7cfdc93eba0fb1612ef510fc0b013919968152714eb29929
            fe4b74099872ba6e5071704899741597fb2b0234052e9c343d746a0db4497e95)

prepare() {
  cd $_pkgfn
  # upstream suggested backport to fix a bug where applet popups don't appear when clicking their icons in the panel.
  git cherry-pick -n e4556c59f0c8250da7c16759432b2ac0a5ac9d9f
  patch -p1 < "$srcdir"/0001-text-input-v3-use-dashed-underline-instead-of-solid-.patch
  patch -p1 < "$srcdir"/0002-text-input-v3-always-update-cursor-rectangle-after-e.patch
  patch -p1 < "$srcdir"/0005-fix-egl-compositor-shutdown.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$_pkgname
}
