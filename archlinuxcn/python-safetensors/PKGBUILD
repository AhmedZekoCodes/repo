# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=safetensors
pkgname=python-safetensors
pkgver=0.5.1
pkgrel=1
epoch=1
pkgdesc='Simple, safe way to store and distribute tensors'
arch=('x86_64')
url='https://github.com/huggingface/safetensors'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-setuptools
  python-setuptools-rust
  python-wheel
)
optdepends=(
  python-numpy
  python-pytorch
  python-tensorflow
  python-huggingface-hub
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/safetensors/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('c20152d644efd39db938dd4ca210607ec8d75bfd9de5377c3adc1f3d7fd64dc52ba4d546163f520b03eef47f3379857eb46dd4c404e358e9c1f71dad2f5404a3')

build() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
