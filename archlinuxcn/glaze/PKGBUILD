# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Michael Bolden Jnr / SM9(); <me@sm9.dev>

pkgname=glaze
pkgver=4.3.1
pkgrel=1
pkgdesc="An Extremely fast, In-Memory JSON and Interface Library for Modern C++"
arch=('any')
url="https://github.com/stephenberry/${pkgname}"
license=('MIT')
makedepends=('git' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('934b16d0a00dfc2b06bc2920a6444b8a6a34edb4f0fe872f8ad7a044518a37e3')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${pkgname}-${pkgver}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp -r docs/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
