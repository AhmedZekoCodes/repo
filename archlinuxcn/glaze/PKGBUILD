# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Michael Bolden Jnr / SM9(); <me@sm9.dev>

pkgname=glaze
pkgver=4.2.4
pkgrel=1
pkgdesc="An Extremely fast, In-Memory JSON and Interface Library for Modern C++"
arch=('any')
url="https://github.com/stephenberry/${pkgname}"
license=('MIT')
makedepends=('git' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('dd409d60b60cb65c7a71ade6331a649710d0eb3d56c66c3bfd461e837359cc2a')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${pkgname}-${pkgver}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp -r docs/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
