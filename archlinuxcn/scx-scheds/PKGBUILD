# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=1.0.4
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'cargo' 'jq')
backup=('etc/default/scx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e47840f58dfc5e3c03c04352a72b73d87da0e2b494afd4666af1136cd666c2c67553810a1e1aa26fbb83fa817698e09d3e2a3f564d816bf8eaf6f1e9f78ff9b0')
options=('!lto')

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build -Dcargo_home=$HOME/cargo-deps --auto-features auto
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
