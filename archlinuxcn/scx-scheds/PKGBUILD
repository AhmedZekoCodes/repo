# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=1.0.5
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'cargo' 'jq')
backup=('etc/default/scx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('05517e5d796fb79e8625b17c3ffedafa2eebc17501e05c1fb99319282729e4cae9cfcfbf32c8034d7b4c6ed20f7e37dcbd9db8d074c49eb70185b6186f7db827')
options=('!lto')

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build -Dcargo_home=$HOME/cargo-deps --auto-features auto
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
