# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=1.0.6
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'cargo' 'jq')
backup=('etc/default/scx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('16e9960a9411a6f9ed9f3b1a8c70eb8535c5fe79decfd5fccc11fe6463f227bc22a434f3509e14d6e49c66e658d03792f9cb9253bae4875f686310e4a45d5e06')
options=('!lto')

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build -Dcargo_home=$HOME/cargo-deps --auto-features auto
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
