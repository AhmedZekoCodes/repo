# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Martin Hundeb√∏ll <martin@hundeboll.net>

pkgname=tio
pkgver=3.9
pkgrel=1
pkgdesc="The simple TTY terminal I/O application"
url="https://github.com/tio/tio"
arch=('x86_64' 'i686' 'arm' 'armv7h' 'armv6h' 'aarch64')
license=('GPL-2.0-or-later')
depends=(
  'glib2'
  'glibc'
  'lua'
)
makedepends=(
  'bash-completion'
  'meson'
)
source=("https://github.com/tio/tio/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
b2sums=('eee634fb540a2910706d9aec686e386727cc665bff9d5febe438e08a38d2df3c2faab64bc9e87757caf8758f456a79a8ef18e1f7c1601be40ad1f96895cfc36b')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  depends+=('libglib-2.0.so')
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
