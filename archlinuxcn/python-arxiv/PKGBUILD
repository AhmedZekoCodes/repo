# Maintainer: Mateen Ulhaq <mulhaq2005+aur at gmail dot com>

_pkgname=arxiv.py
pkgname=python-arxiv
pkgver=2.1.0
pkgrel=1
pkgdesc="Python wrapper for the arXiv API: https://info.arxiv.org/help/api/"
arch=('any')
url="https://github.com/lukasschwab/${_pkgname}"
license=('MIT')
depends=(
    'python'
    'python-feedparser'
    'python-requests'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('f3e3617f250dade5e958271daa26622018dd4bdd3a8cd57b9b1db335d8b1f1f8')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
