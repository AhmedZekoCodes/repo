# Maintainer: Mateen Ulhaq <mulhaq2005+aur at gmail dot com>

_pkgname=arxiv.py
pkgname=python-arxiv
pkgver=2.2.0
pkgrel=1
pkgdesc="Python wrapper for the arXiv API: https://info.arxiv.org/help/api/"
arch=('any')
url="https://github.com/lukasschwab/${_pkgname}"
license=('MIT')
depends=(
    'python'
    'python-feedparser'
    'python-requests'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('184c51b2f0d4cd3bf8998a03b9ab09ed5e516a9c9cf5bcc5057bf8f0cae2413e')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
