# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Michael Bromilow <dev at mail subdomain of bromilow dot uk>
# Contributor: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

_pkgname=rectangle-packer
pkgname="python-${_pkgname}"
pkgver=2.0.4
pkgrel=1
pkgdesc="Pack a set of rectangles into a bounding box with minimum area"
arch=('x86_64')
url="https://github.com/Penlect/${_pkgname}"
license=('MIT')
depends=(
    'glibc'
    'python'
)
makedepends=(
    'cython'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
b2sums=('03ce2b978e645454dbf8a3d863b46fbe4b344f5b0d32e27be6be49d1bcd6706ea6993e02a961fc715bdb99b7f47b86093f111efc311acf1cf8d2351d1371f338')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -Dm0644 "LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
