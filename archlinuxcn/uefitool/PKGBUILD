# Maintainer: Yurii Kolesnykov <root@yurikoles.com>
# Based on uefitool-ng-git by Bailey Fox <bfox200012@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=uefitool
_pkgname=UEFITool
pkgver=A68
pkgrel=2
epoch=2
pkgdesc="UEFI firmware image viewer and editor and utilities"
arch=('armv7h' 'aarch64' 'i686' 'x86_64')
url="https://github.com/LongSoft/${_pkgname}"
license=('BSD-2-Clause')
depends=('qt6-base')
makedepends=('cmake')
provides=('uefipatch')
conflicts=('uefipatch')
replaces=('uefipatch')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('aa4bc52105ab83e5b094de9b54b31d52a30ae1c0f3f1ce7930088fc412efe900')

build() {
	cmake -B build -S "${_pkgname}-${pkgver}" -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
