# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=datasets
pkgname=python-datasets
pkgver=3.1.0
pkgrel=1
pkgdesc='The largest hub of ready-to-use datasets for ML models with fast, easy-to-use and efficient data manipulation tools'
arch=('any')
url='https://github.com/huggingface/datasets'
license=('Apache-2.0')
depends=(
  python-aiohttp
  python-dill
  python-filelock
  python-fsspec
  python-huggingface-hub
  python-multiprocess
  python-numpy
  python-packaging
  python-pandas
  python-pyarrow
  python-requests
  python-tqdm
  python-xxhash
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/datasets/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('7e2a22b6ca423f4358153137f50d963075d37ad01e834c78176dc432f1b3adc3b80df44ac35b22d727db4e1aa8331c938efca1ab2d678e58401e8e7ce571abfc')

prepare() {
  # pyarrow_hotfix is only used for pyarrow < 14.0.1
  sed -i "/pyarrow_hotfix/d" "${srcdir}/${_pkgname}-${pkgver}/src/datasets/features/features.py"
  sed -i "/pyarrow-hotfix/d" "${srcdir}/${_pkgname}-${pkgver}/setup.py"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
