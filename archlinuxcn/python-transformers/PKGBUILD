# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=transformers
pkgname=python-transformers
pkgver=4.46.1
pkgrel=1
pkgdesc='State-of-the-art Machine Learning for Pytorch, TensorFlow, and JAX'
arch=('any')
url='https://github.com/huggingface/transformers'
license=('Apache')
depends=(
  python-filelock
  python-huggingface-hub
  python-numpy
  python-regex
  python-requests
  python-sacremoses
  python-tokenizers
  python-tqdm
  python-yaml
)
makedepends=(
  python-setuptools
)
optdepends=(
  python-pytorch
  python-tensorflow
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/transformers/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3b84e1eddc68e3e50d1b3e91e8fc84c62e2f66caf93eccbd5aa848c28cc04507b3cccfe0c49547a4215b490da89463fe6d2a254293d825870e75d0f726556b81')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
