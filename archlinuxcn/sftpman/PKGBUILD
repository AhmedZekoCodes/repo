# Maintainer: Roald Clark <roaldclark at gmail.com>
# Contributor: Slavi Pantaleev <slavi at devture.com>

_name=sftpman-rs
pkgname=sftpman
pkgver=2.0.6
pkgrel=1
pkgdesc="A CLI application for managing and mounting sshfs (SFTP) filesystems"
arch=('x86_64' 'aarch64')
url="https://github.com/spantaleev/${_name}"
license=('AGPL-3.0-or-later')
depends=(
    'fuse3'
    'gcc-libs'
    'glibc'
    'sshfs'
)
makedepends=(
    'cargo'
    'git'
)
optdepends=('sftpman-iced: GUI frontend for sftpman v2+')
conflicts=('sftpman-python')
replaces=('sftpman-python')
install="${pkgname}.install"
source=("git+${url}.git#tag=v${pkgver}")
b2sums=('1f71be899abdd88a70735595a5cc752498a686826d6eea44b4e049d007f45a453341eedb545f683f5104e53c33a9e2f2054d96b3b2a04299723b7932085aa735')

prepare() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen --all-features
}

check() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "$srcdir/${_name}"
    install -Dm0755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
    install -Dm0644 etc/bash_completion.d/sftpman -t "${pkgdir}/usr/share/bash-completion/completions/"
    install -Dm0644 etc/fish-completions/sftpman.fish -t "${pkgdir}/usr/share/fish/completions/"
}
