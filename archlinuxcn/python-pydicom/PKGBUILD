# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=2.4.4
pkgrel=2
pkgdesc='Read, modify and write DICOM files with python code'
arch=(any)
url='https://github.com/pydicom/pydicom'
license=(MIT)
depends=(
  python
)
optdepends=(
  'gdcm: for working with compressed JPEG, JPEG-LS and JPEG 2000 images'
  'python-numpy: for working with pixel data'
  'python-pillow: for working with compressed image data'
  'python-pylibjpeg-openjpeg: or working with compressed JPEG-LS or JPEG 2000 images'
  'python-pylibjpeg-rle: for working with compressed RLE images'
  'python-pylibjpeg: for working with compressed JPEG images'
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
sha512sums=('566f3bedabc56606f087601038b5477a76c407809b4b74c61af7e0cc9e0994025f0d6294903ad89e8f4c0302d777ba4a28d5be63337046136c9aef5abe104a0c')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
