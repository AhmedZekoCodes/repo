# Maintainer: Coelacanthus <uwu@coelacanthus.name>

pkgbase=ttf-plangothic
pkgname=(ttf-plangothic otf-plangothic)
epoch=2
pkgver=1.9.5769
pkgrel=1
pkgdesc='A project based on SourceHan to expand and supplement characters, and its ultimate goal is to complete the entire Unicode as much as possible.'
url='https://github.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic'
arch=('any')
license=('OFL-1.1')
replaces=('ttf-plangothic-mob')
source=(
    $pkgbase-$pkgver-PlangothicP1-Regular.otf::https://github.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic/releases/download/V$pkgver/PlangothicP1-Regular.fallback.otf
    $pkgbase-$pkgver-PlangothicP1-Regular.ttf::https://github.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic/releases/download/V$pkgver/PlangothicP1-Regular.fallback.ttf
    $pkgbase-$pkgver-PlangothicP2-Regular.otf::https://github.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic/releases/download/V$pkgver/PlangothicP2-Regular.otf
    $pkgbase-$pkgver-PlangothicP2-Regular.ttf::https://github.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic/releases/download/V$pkgver/PlangothicP2-Regular.ttf
    $pkgbase-$pkgver-LICENSE::https://raw.githubusercontent.com/Fitzgerald-Porthmouth-Koenigsegg/Plangothic/V$pkgver/LICENSE
)
b2sums=('2898d275fe536fb9d2fe0e8b7db94a9c34826a8e980da7131c64f1aebbeb0fa92d0902e1f2accd10edea08d7bff9f374308789072c153b043e8e642d5d98fd55'
        'e17856bd5cd263bd369cb4094d2c4180e9293b9d641f0f3d55b5ac0540bf2e72db8b9bbc2f3e32a5a2585a79b4268541abd7a5fd5a709efbc4504c458c94a176'
        'c424b13b7827c1118ab2f53f3643e0236dc191ccdd8da8f36698cb6f440b07d47f6250735071ca1fe6ea1b9eb915118d7dcf290b560f6736e8280a13e9b9c388'
        '3cee167d9923f59593de0701746af7199347ec91a607333bf4b6f8c98b30a350fe7a8b4c075579c7853e93a4161453211fec96f37a66d14ac59cc89e009b02bb'
        'c685b6f4f9c6c9042c12834df568f835155387d32a41b30ae524fa28acd569245a01c02e6c9ba7ef5012eca88536155236bbf36cf243e6d658a33d11bc7552ae')

package_otf-plangothic() {
    install -Dm755 -d "$pkgdir/usr/share/fonts/OTF/Plangothic"
    install -Dm644 "$pkgbase-$pkgver-PlangothicP1-Regular.otf" "$pkgdir/usr/share/fonts/OTF/Plangothic/PlangothicP1-Regular.otf"
    install -Dm644 "$pkgbase-$pkgver-PlangothicP2-Regular.otf" "$pkgdir/usr/share/fonts/OTF/Plangothic/PlangothicP2-Regular.otf"
    install -Dm644 "$pkgbase-$pkgver-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
package_ttf-plangothic() {
    install -Dm755 -d "$pkgdir/usr/share/fonts/TTF/Plangothic"
    install -Dm644 "$pkgbase-$pkgver-PlangothicP1-Regular.ttf" "$pkgdir/usr/share/fonts/TTF/Plangothic/PlangothicP1-Regular.ttf"
    install -Dm644 "$pkgbase-$pkgver-PlangothicP2-Regular.ttf" "$pkgdir/usr/share/fonts/TTF/Plangothic/PlangothicP2-Regular.ttf"
    install -Dm644 "$pkgbase-$pkgver-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim: set ts=4 sw=4 et:
