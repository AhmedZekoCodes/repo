# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

_pkgname=qqc2-desktop-style
pkgname="${_pkgname}-fractional"
pkgver=6.8.0
_pkgver=${pkgver/rc/-rc}
pkgrel=2
pkgdesc="A style for Qt Quick Controls 2 to make it follow your desktop theme (with Qt text rendering patch)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://community.kde.org/Frameworks"
license=('LGPL-2.0-only OR LGPL-3.0-only')
depends=('gcc-libs'
	'kcolorscheme'
	'kconfig'
	'kiconthemes'
	'kirigami'
	'qt6-base'
	'qt6-declarative'
	'sonnet')
makedepends=('extra-cmake-modules-git' 'qt6-tools')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("https://invent.kde.org/frameworks/${_pkgname}/-/archive/v${_pkgver}/${_pkgname}-v${_pkgver}.tar.gz")
sha256sums=('34f092c2fc258bb21326f9e752746fb7a826beba38345415124aa03e5b9198c2')

build() {
	cmake -B build \
		-S "${_pkgname}-v${_pkgver}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_TESTING=OFF

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 ${_pkgname}-v${_pkgver}/LICENSES/* -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
