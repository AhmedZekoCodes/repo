# Maintainer: Gavin Luo <lunt.luo#gmail.com>
# Contributor: Victor Bayas <victorsbayas at gmail dot com>
# Contributor: Victor Bayas <victorsbayas at gmail dot com>

pkgname=envycontrol
pkgver=3.5.2
pkgrel=1
pkgdesc="CLI tool for Nvidia Optimus graphics mode switching on Linux"
arch=('any')
url="https://github.com/bayasdev/envycontrol"
license=('MIT')
depends=('python' 'xorg-xrandr')
makedepends=('python-setuptools')
conflicts=('optimus-manager')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ad42c61161e10d6c2922261589b9f27015888eed73268fb02da0aa2f319e3eaa69205f5dfbe2dfc5bea36116e1aecb0bd9d0939202387275043f05f47161aa9c')

package() {
  cd "${srcdir}/envycontrol-${pkgver}/"

  python setup.py install --root="$pkgdir/" --optimize=1

  # Copy package license to system
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
