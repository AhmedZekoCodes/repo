# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Peter Jung <ptr1337@archlinux.org>

pkgname=nvidia-open-clear-x64-v3
pkgver=565.77
pkgrel=1
pkgdesc="NVIDIA open kernel modules for linux-clear-x64-v3"
arch=('x86_64')
url="https://github.com/NVIDIA/open-gpu-kernel-modules"
license=('MIT AND GPL-2.0-only')
depends=(
    "libglvnd"
    "nvidia-utils=${pkgver}"
)
makedepends=(
    "linux-clear-x64-v3-headers"
    "nvidia-open-dkms=${pkgver}"
)
provides=("NVIDIA-MODULE=${pkgver}")
conflicts=('nvidia-open-dkms')
options=('!strip')

build() {
    _kernver=$(</usr/src/linux-clear-x64-v3/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m "nvidia/${pkgver}" -k "${_kernver}"
}

package() {
    depends+=("linux-clear-x64-v3=6.12.1-1")

    _kernver=$(</usr/src/linux-clear-x64-v3/version)

    install -Dm0644 "nvidia/${pkgver}/${_kernver}/${CARCH}/module/"*.ko -t "${pkgdir}/usr/lib/modules/${_kernver}/extramodules"

    # compress each module individually
    find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +

    install -Dm0644 /usr/share/licenses/nvidia-open-dkms/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
