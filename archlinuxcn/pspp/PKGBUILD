# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>

pkgname=pspp
pkgver=2.0.1
pkgrel=1
pkgdesc='Statistical analysis program. Free replacement for SPSS'
arch=('x86_64')
url='http://www.gnu.org/software/pspp'
license=('GPL-3.0-or-later')
depends=(
  gsl
  gtksourceview3
  postgresql-libs
  spread-sheet-widget
)
makedepends=(
  gcc13
  glib2-devel
  python
)
optdepends=(
  'zlib: GNUmeric support'
  'libxml2: GNUMERIC support'
)
options=('!libtool' '!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::https://ftp.gnu.org/gnu/pspp/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::https://ftp.gnu.org/gnu/pspp/${pkgname}-${pkgver}.tar.gz.sig"
)
sha256sums=('8edbb0f09e8cf8010cad9e0559e0230d7fc5aae4721c756c350554df33024c00'
            'SKIP')
validpgpkeys=('C2D1AB061656AAC54B5E975485199DE8C6648E90')
build() {
  cd "${pkgname}-${pkgver}"
  CC=gcc-13 \
  CXX=g++-13 \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
