# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Paul <pb.orzel@proton.me>

pkgname=amdgpu_top
pkgver=0.10.3
pkgrel=1
pkgdesc="Tool that shows AMD GPU utilization"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/Umio-Yasuno/${pkgname}"
license=("MIT")
depends=("libdrm")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b74065ea89fb35a572dddccc22b71e81b7cb1e76b83027704814595e4fcdc6e4')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 assets/${_pkgname}{,-tui}.desktop -t "${pkgdir}/usr/share/applications/desktop/"

	install -d "${pkgdir}/usr/share/doc/${_pkgname}/"
	cp -r docs/* "${pkgdir}/usr/share/doc/${_pkgname}/"

	install -Dm644 "docs/${_pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
