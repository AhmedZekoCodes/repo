# Maintainer: Integral <integral@member.fsf.org>

pkgname=clapgrep
pkgver=25.02
pkgrel=1
pkgdesc="One app to search through all your files"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/luleyleo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gtk4' 'libadwaita' 'gtksourceview5' 'poppler-glib')
makedepends=('git' 'meson' 'cargo' 'blueprint-compiler')
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
	"fix-archlinux-build.patch::${url}/pull/83.patch"
)
sha256sums=('a05d3ac660ee9e38e091b9a7af3dad2a80e08dc7fa19a6570d7fc75464c4854b'
            '064e7be0c3baf4005936ada2af0bfa4df7f2c24156ff7b374ac42f449c6d5db1')

prepare() {
	patch -d "${pkgname}-${pkgver}/" -p1 -i ../fix-archlinux-build.patch
}

build() {
	arch-meson "${pkgname}-${pkgver}/" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
