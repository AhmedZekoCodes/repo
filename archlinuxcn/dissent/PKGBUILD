# Maintainer: txtsd <aur.archlinux@ihavea.quest>

pkgname=dissent
pkgver=0.0.31
_pkgver=$(sed 's/\./-/3' <<<${pkgver})
pkgrel=1
pkgdesc="Discord client written in go and gtk4"
arch=('x86_64' 'aarch64')
url="https://github.com/diamondburned/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gtk4' 'gobject-introspection' 'libadwaita' 'gtksourceview5' 'libspelling')
makedepends=('git' 'go')
provides=("gtkcord4")
replaces=("gtkcord4")
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${_pkgver}.tar.gz"
	"https://github.com/diamondburned/dissent/pull/336.patch"
)
sha256sums=('0e7ce9abfa6f8fb4c2c88a78ec18a84403d706ef08ceec955d173223835cb17d'
            'b27f56151ae919aba2e88c4676b5f1ff1300fe3c4eb084c61dd5fe6793ff6c80')

prepare() {
	cd "${pkgname}-${_pkgver}/"
	mkdir -p build

	# Apply patch to update libspelling go mods
	patch -p1 -i ../336.patch
}

build() {
	cd "${pkgname}-${_pkgver}/"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}/go"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o build
}

package() {
	cd "${pkgname}-${_pkgver}/build/"
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"

	cd ..
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 "nix/so.libdb.${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm644 "internal/icons/hicolor/scalable/apps/so.libdb.${pkgname}.Devel.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
