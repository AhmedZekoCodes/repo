# Maintainer: Ren√© Wagner <rwa at clttr dot info>

pkgname=art-rawconverter
_pkgname=ART
pkgver=1.24.1
pkgrel=1
pkgdesc="raw image converter ART (forked from RawTherapee with ease of use in mind)"
arch=('i686' 'x86_64' 'aarch64')
url="https://bitbucket.org/agriggio/art/wiki/Home"
license=('GPL-3.0-or-later')
depends=('opencolorio' 'lensfun' 'libraw' 'exiv2' 'fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'libcanberra' 'libiptcdata' 'desktop-file-utils' 'mimalloc' 'openmp')
optdepends=('perl-image-exiftool: metadata support for CR3 images'
	'art-rawconverter-imageio: add support for additional image formats'
	'lcms2-ff: lcms2 with fast-float plugin for improved export speed')
makedepends=('pkgconf' 'cmake' 'hicolor-icon-theme')
source=("https://bitbucket.org/agriggio/art/downloads/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('27fd10557ef147b91d1719766bc7ae4b1ac01c5d06d6d8f8bcdb69ace726b9b3')
options=('!lto')

prepare() {
	mkdir -p "${_pkgname}-${pkgver}/build/"
}

build() {
	cd "${_pkgname}-${pkgver}/build/"

	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DPROC_TARGET_NUMBER="2" \
		-DENABLE_LIBRAW="ON" \
		-DENABLE_OCIO="ON" \
		-DBUILD_SHARED="ON"

	make
}

package() {
	cd "${_pkgname}-${pkgver}/"
	make DESTDIR="${pkgdir}" -C build install

	install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	cp -r licenses/* "${pkgdir}/usr/share/licenses/${pkgname}/"
}
