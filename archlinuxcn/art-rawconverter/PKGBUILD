# Maintainer: Ren√© Wagner <rwa at clttr dot info>

pkgname=art-rawconverter
pkgver=1.25.2
pkgrel=1
pkgdesc="raw image converter ART (forked from RawTherapee with ease of use in mind)"
arch=('i686' 'x86_64' 'aarch64')
url="https://bitbucket.org/agriggio/art"
license=('GPL-3.0-or-later')
depends=('opencolorio' 'lensfun' 'libraw' 'exiv2' 'fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'libcanberra' 'libiptcdata' 'desktop-file-utils' 'mimalloc' 'openmp')
optdepends=('perl-image-exiftool: metadata support for CR3 images'
	'art-rawconverter-imageio: add support for additional image formats'
	'lcms2-ff: lcms2 with fast-float plugin for improved export speed')
makedepends=('cmake' 'hicolor-icon-theme')
source=("${pkgname}-${pkgver}.tar.gz::${url}/get/${pkgver}.tar.gz")
sha256sums=('b8f509ff26d2d194b22a9d62e6950a47839b99c84b6c1a600131e58041a337aa')
options=('!lto')

build() {
	cmake -B build -S $(basename agriggio-art-*) \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None \
		-DPROC_TARGET_NUMBER="2" \
		-DENABLE_LIBRAW="ON" \
		-DENABLE_OCIO="ON" \
		-DBUILD_SHARED="ON"

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd agriggio-art-*
	install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	cp -r licenses/* "${pkgdir}/usr/share/licenses/${pkgname}/"
}
