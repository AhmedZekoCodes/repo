# Maintainer: oldherl <oldherl@gmail.com>
# Contributor: Miguel de Val-Borro <miguel dot deval at gmail dot com>
# Contributor: Astro Benzene <universebenzene at sina dot com>

pkgname=astrometry.net-znver2
_pkgname=astrometry.net
pkgver=0.95
pkgrel=5
pkgdesc="Automatic recognition of astronomical images. Compiled with -march=znver2 for AMD Ryzen CPUs."
arch=('x86_64')
url="https://astrometry.net/"
license=("GPL-3.0-or-later")
provides=("astrometry.net")
conflicts=("astrometry.net")
depends=('bzip2' 'cairo' 'libpng' 'libjpeg-turbo' 'python-astropy' 'netpbm' 'cfitsio' 'gsl' 'swig')
makedepends=('python-setuptools')
optdepends=(
'astrometry.net-data-4108-wide: index data for wide field'
'astrometry.net-data-5007: index data for features 22 to 30 arcmin'
'astrometry.net-data-5006: index data for features 16 to 22 arcmin'
)
backup=(etc/astrometry.cfg)
install=astrometry.net.install
source=("https://github.com/dstndstn/astrometry.net/releases/download/${pkgver}/astrometry.net-${pkgver}.tar.gz"
"https://github.com/dstndstn/astrometry.net/pull/298.patch"
"https://github.com/dstndstn/astrometry.net/pull/299.patch"
)
sha1sums=('984de6879215794d4fec7916fa82741367d3b10d'
          '0af88525e2e87ed2141ed072039d26fe464d3612'
          'd3450d5e88f62f7f6c681f12156816f6d30bb225')

prepare() {
    cd ${_pkgname}-${pkgver}
    # for fixing numpy 2.0
    patch -p1 -i ../298.patch
    patch -p1 -i ../299.patch
    rm util/util_wrap.c
#    grep -RnI elsize . || echo 0
}

build() {
    cd ${_pkgname}-${pkgver}
    # Parrallel build is broken and upstream is not going to fix it.
    # https://github.com/dstndstn/astrometry.net/issues/178#issuecomment-650149535
    ARCH_FLAGS='-march=znver2' make -j 1 SYSTEM_GSL=yes all py extra
}

check() {
    cd ${_pkgname}-${pkgver}
    make -j 1 SYSTEM_GSL=yes test
}

package() {
    cd ${_pkgname}-${pkgver}
    _pyver=$(python -c 'import sys; print("%d.%d" % sys.version_info[:2])')

    make INSTALL_DIR="${pkgdir}/usr" \
         ETC_INSTALL_DIR="${pkgdir}/etc" \
         DOC_INSTALL_DIR="${pkgdir}/usr/share/doc/astrometry" \
         EXAMPLE_INSTALL_DIR="${pkgdir}/usr/share/astrometry/examples" \
         DATA_INSTALL_DIR="${pkgdir}/usr/share/astrometry/data" \
         PY_BASE_INSTALL_DIR="${pkgdir}/usr/lib/python${_pyver}/site-packages/astrometry" \
         PY_BASE_LINK_DIR="../lib/python${_pyver}/site-packages/astrometry" \
         install

    # fix data path and enable inparallel by default
    sed -e "s|${pkgdir}/usr/data|/usr/share/astrometry/data|" -e 's|#inparallel|inparallel|' -i ${pkgdir}/etc/astrometry.cfg
    # remove useless file
    rm ${pkgdir}/usr/share/doc/astrometry/report.txt
    # those executables are included in cfitsio package
    rm ${pkgdir}/usr/bin/fitscopy
    rm ${pkgdir}/usr/bin/imcopy
    rm ${pkgdir}/usr/bin/fitsverify
}
