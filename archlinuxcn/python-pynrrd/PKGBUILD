# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Martino Pilia <martino.pilia@gmail.com>

_pkgname=pynrrd
pkgname=python-pynrrd
pkgver=1.1.1
pkgrel=1
pkgdesc='Simple pure-python module for reading and writing nrrd files'
arch=('any')
url='https://github.com/mhe/pynrrd'
license=('MIT')
depends=(
  python-numpy
  python-typing_extensions
)
makedepends=(
  python-build
  python-setuptools
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mhe/pynrrd/archive/v${pkgver}.tar.gz")
sha256sums=('698fbf3384ce2f5637cb6c2991917f2208fccaeaebc0fdc20ab5fa0a8666c0c4')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pkgname}-${pkgver}/nrrd"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rfv ${pkgdir}${site_packages}/docs
}
# vim:set ts=2 sw=2 et:
