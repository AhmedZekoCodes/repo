# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Peter Cai <peter@typeblog.net>

pkgname=lpac
pkgver=2.2.0
pkgrel=1
pkgdesc="Local Profile Agent (LPA) for eSIM cards via PC/SC readers, or AT / APDU commands over stdio."
arch=(x86_64 aarch64 armv7h)
url="https://github.com/estkme-group/lpac"
license=('AGPL-3.0-only AND LGPL-2.0-only AND MIT')
depends=(curl pcsclite glibc)
makedepends=(cmake ninja)
source=($pkgname-$pkgver.tar.gz::https://github.com/estkme-group/lpac/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('bf14823981b7b8c50fa41fd3e6210d753c13479a468c343e3205a7558a4a6b63546fdac3705aa46f317d47aa60111cf11ab025ad57d1ba6d11b04faa7dda0599')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLPAC_DYNAMIC_LIBEUICC=ON \
    -DLPAC_DYNAMIC_DRIVERS=ON
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"$pkgname" README.md
}

