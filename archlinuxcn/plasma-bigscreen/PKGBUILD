# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=plasma-bigscreen
pkgver=5.27.10
_dirver=$(echo ${pkgver} | cut -d. -f1-3)
pkgrel=1
pkgdesc="Plasma shell for TVs"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://${pkgname}.org"
license=('LGPL-2.0-or-later')
depends=('plasma-pa' 'plasma-nm' 'plasma-nano' 'plasma-settings' 'kdeconnect' 'qt5-virtualkeyboard' 'plasma-remotecontrollers')
makedepends=('extra-cmake-modules')
optdepends=('aura-browser: web browser' 'plank-player: media player')
source=("https://download.kde.org/stable/plasma/${_dirver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3731ab079b820e5c6c288343013c5667e82d64db869a1c84ffe549e9ee068218')

build() {
	cmake -B build -S "${pkgname}-${pkgver}/" \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_TESTING=OFF

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
