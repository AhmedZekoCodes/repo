# Maintainer: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm
pkgver=1.1.1
pkgrel=1
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'qt6-tools'
	'qt6-declarative'
	'qt6-shadertools'
	'gcc-libs'
	'glibc'
	'dbus'
	'libnghttp2'
	'libidn2'
	'krb5'
	'zstd'
	'zlib'
	'qt6-wayland'
	'libx11'
	'libxkbcommon'
	'libpng'
	'freetype2'
	'icu'
	'pcre2'
	'gnutls'
	'graphite'
	'lz4'
	'libgpg-error'
	'ffmpeg')
makedepends=(
	'git'
	'cmake'
	'extra-cmake-modules'
	'ninja')
source=(
	"git+${url}.git#tag=v${pkgver}"
	"${pkgname}-fix-qt6.8-build.patch::${url}/pull/36.patch"
)
sha256sums=('639978c7c56be70aed1b16c444d6f577310942283be7951cf647f3ad34ca5af3'
            '495a50534960a0f46b6e843543dd06d82f4c7514dd78122a074191f759ba6316')

prepare() {
	cd Qcm
	git submodule update --init --recursive
	patch -p1 <"../${pkgname}-fix-qt6.8-build.patch"
	mkdir build
}

build() {
	cd Qcm/build
	cmake .. -GNinja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	ninja
}

package() {
	cd Qcm
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
