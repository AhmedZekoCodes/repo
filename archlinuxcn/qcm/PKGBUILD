# Maintainer: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm
pkgver=1.0.5
pkgrel=3
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'qt6-tools'
	'qt6-declarative'
	'qt6-shadertools'
	'gcc-libs'
	'glibc'
	'dbus'
	'libnghttp2'
	'libidn2'
	'krb5'
	'zstd'
	'zlib'
	'qt6-wayland'
	'libx11'
	'libxkbcommon'
	'libpng'
	'freetype2'
	'icu'
	'pcre2'
	'gnutls'
	'graphite'
	'lz4'
	'libgpg-error'
	'ffmpeg')
makedepends=(
	'git'
	'cmake'
	'extra-cmake-modules'
	'ninja')
source=(
	"git+${url}.git#tag=v${pkgver}"
	{qcm,qml-material}-cmakelists.patch
)
sha256sums=('07f80869a0a602500e3b75605375c449b822bb3dae652feac7a68ceb7e8b1a22'
            'fa8c036089de9a548e9202d6426ceaf42ffe2424fe3a4c1b4840e0b5ffb21c09'
            '60444e9b7c3f1285074c5880a3ef76532a1ff1752a688e0e7c10ced35cbaccc6')

prepare() {
	cd Qcm
	git submodule update --init --recursive
	mkdir build

	patch -p1 < ../qcm-cmakelists.patch
	cd qml_material
	patch -p1 < ../../qml-material-cmakelists.patch
}

build() {
	cd Qcm/build
	cmake .. -GNinja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	ninja
}

package() {
	cd Qcm

	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
