# Maintainer: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm
pkgver=1.0.5
pkgrel=4
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'qt6-tools'
	'qt6-declarative'
	'qt6-shadertools'
	'gcc-libs'
	'glibc'
	'dbus'
	'libnghttp2'
	'libidn2'
	'krb5'
	'zstd'
	'zlib'
	'qt6-wayland'
	'libx11'
	'libxkbcommon'
	'libpng'
	'freetype2'
	'icu'
	'pcre2'
	'gnutls'
	'graphite'
	'lz4'
	'libgpg-error'
	'ffmpeg')
makedepends=(
	'git'
	'cmake'
	'extra-cmake-modules'
	'ninja')
source=(
	"git+${url}.git#tag=v${pkgver}"
	"qcm.patch::https://github.com/hypengw/Qcm/pull/34.patch"
	"qml-material.patch::https://github.com/hypengw/QmlMaterial/pull/1.patch"
)
sha256sums=('07f80869a0a602500e3b75605375c449b822bb3dae652feac7a68ceb7e8b1a22'
            'ac714b4d3f3647ebe7254fb574df28e78bc78f54b90931c40278fa9e2f6c294c'
            'a77a2ada3fcce697658745cfac47a1ac7fdf9f2ff4a367c5cb65e7ef18ddf343')

prepare() {
	cd Qcm
	git submodule update --init --recursive
	mkdir build

	patch -p1 < ../qcm.patch
	cd qml_material
	patch -p1 < ../../qml-material.patch
}

build() {
	cd Qcm/build
	cmake .. -GNinja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	ninja
}

package() {
	cd Qcm
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
