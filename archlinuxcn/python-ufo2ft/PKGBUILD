# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_name=ufo2ft
pkgname="python-${_name}"
pkgver=3.3.0
pkgrel=1
pkgdesc="A bridge from UFOs to FontTools objects"
arch=('any')
url="https://github.com/googlefonts/${_name}"
license=('MIT')
depends=(
    'python'
    'python-booleanoperations'
    'python-cffsubr'
    'python-defcon'
    'python-fonttools'
    'python-fs' # for fonttools[ufo]
    'python-lxml' # for fonttools[lxml]
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-setuptools-scm'
    'python-syrupy'
    'python-wheel'
)
checkdepends=(
    'python-compreffor'
    'python-fontmath'
    'python-pytest'
    'python-skia-pathops'
    'python-ufolib2'
)
optdepends=(
    'python-compreffor'
    'python-skia-pathops'
)
source=("${pkgname}-${pkgver}::git+${url}.git#tag=v${pkgver}"
        "python-ufo2ft-331-fix-tests.patch")
sha256sums=('52449dce1c070546e6e019758acbf49b7c85bff0eced5f41845768ee0c374637'
            '5665c499d39be6c19298d6bcd6d093367e185a1d27f5b4b76117432778672709')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i ../python-ufo2ft-331-fix-tests.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m venv --system-site-packages .venv
    .venv/bin/python -m installer dist/*.whl
    .venv/bin/python -m pytest -v tests
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
