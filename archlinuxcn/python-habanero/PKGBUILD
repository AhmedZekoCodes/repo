# Maintainer: Gesh <gesh@gesh.uni.cx>
# Contributor: JP-Ellis <josh@jpellis.me>

pkgname='python-habanero'
pkgver='2.0.0'
pkgrel=3
_name=${pkgname#python-}
_name="${_name//-/_}"
_src_folder="${_name}-${pkgver}"
pkgdesc="A low level client for Crossref's Search API"
url="https://github.com/sckott/habanero"
depends=(
  'python'
  'python-httpx'
  'python-packaging'
  'python-pyyaml'
  'python-tqdm'
  'python-urllib3'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-hatchling'
)
checkdepends=(
  'python-pytest'
  'python-pytest-cov'
  'python-pytest-recording'
)
optdepends=(
  'python-bibtexparser: attempt to fix misformatted bibtex'
)
license=('MIT')
arch=('any')
_patchUrl="${url/github.com/patch-diff.githubusercontent.com\/raw}"
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
    "${pkgname}-PR199.patch::${_patchUrl}/pull/199.patch"
)
sha256sums=('346561bdac9c985b0b2b1309559eec826a03fc77382030a1e9ddd861e5684e5f'
            '12badd7d4b7cf52474f7505349b60bc62055d165277767abdbd35c2da77bd1f1')

prepare() {
  cd "${srcdir}/${_src_folder}"
  patch -p1 < "../${pkgname}-PR199.patch"
}


build() {
    cd "${srcdir}/${_src_folder}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_src_folder}"
    python -m pytest
}

package() {
    cd "${srcdir}/${_src_folder}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 LICENSE.md -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

# vim:set ts=2 sw=2 et:
