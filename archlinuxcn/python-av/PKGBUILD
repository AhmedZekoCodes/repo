# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=13.1.0
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('941d344d28505d8ae73429383f71eeb58bbe359e8f9b78f8187d88883ff83e3bc306bcef7e77f0dc812815e9c6d3dc18e23d38315cda12d292703ec1057b506d')

build() {
  cd "${_pkgname}-${pkgver}"
  PKG_CONFIG_PATH="/usr/lib/ffmpeg6.1/pkgconfig/" \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
