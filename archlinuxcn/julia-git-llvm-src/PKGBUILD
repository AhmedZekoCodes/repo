pkgname=julia-git-llvm-src
pkgver=9.1.3
_commit=b0e9f6806605a1583e7ccef4f3d2039431959a16
pkgrel=2
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit"
        0001-Generate-julia-wrappers-for-LLVM19.patch
        0002-Update-LLVM-version-check.patch
        0003-Remove-unavailable-constant-expression-functions-on-.patch)
sha256sums=('c0091f6ff13814e693de92c0038ddb2d0d53c4cffd88f8b6b61ce99e3d8b08c1'
            'cf3b5fa715eb29aa047f80f562a996cfb80113bb4b2551d916ef62ac9cbc9ddc'
            '204c5e2d6a1a455d6ff78fa3d08aa22e3284f0ca29e3dced44173633f867a487'
            'cb7b07d1c0289c5ae032074b8f943ab58f2557416bc9061b2facb873e1aedd06')

prepare() {
  cd LLVM.jl
  patch -Np1 --no-backup-if-mismatch < ../0001-Generate-julia-wrappers-for-LLVM19.patch
  patch -Np1 --no-backup-if-mismatch < ../0002-Update-LLVM-version-check.patch
  patch -Np1 --no-backup-if-mismatch < ../0003-Remove-unavailable-constant-expression-functions-on-.patch
}

package() {
  cd LLVM.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh LLVM "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/deps
}
