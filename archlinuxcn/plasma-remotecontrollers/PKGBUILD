# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=plasma-remotecontrollers
pkgver=5.27.10
_dirver=$(echo ${pkgver} | cut -d. -f1-3)
pkgrel=1
pkgdesc="Translate various input device events into keyboard and pointer events"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://${pkgname}.org"
license=('LGPL-2.0-or-later')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'plasma-wayland-protocols' 'libcec')
optdepends=('libcec: TV remotes support')
source=("https://download.kde.org/stable/plasma/${_dirver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('510426dfb47cb86e0491cdeaead3af6f5d8700408973188ca981c24572f1f473')

build() {
	cmake -B build -S "${pkgname}-${pkgver}/" \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_TESTING=OFF

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
