# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm-git
_pkgname=${pkgname%-git}
pkgver=1.2.0_qsql.r2.g6ea43b7
pkgrel=1
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'qt6-quick3d'
	'qt6-grpc'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'dbus'
	'ffmpeg'
	'fmt'
	'cubeb-git'
	'kdsingleapplication')
makedepends=(
	'git'
	'cmake'
	'ninja'
	'asio'
	'pegtl'
	'nlohmann-json'
	'ctre'
	'tl-expected'
	'vulkan-headers')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	git -C Qcm describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cmake -B build \
		-S Qcm \
		-G Ninja \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
