# Maintainer: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm-git
_pkgname=${pkgname%-git}
pkgver=1.1.1.r8.g6fef430
pkgrel=2
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'qt6-tools'
	'qt6-declarative'
	'qt6-shadertools'
	'gcc-libs'
	'glibc'
	'dbus'
	'libnghttp2'
	'libidn2'
	'krb5'
	'zstd'
	'zlib'
	'qt6-wayland'
	'libx11'
	'libxkbcommon'
	'libpng'
	'freetype2'
	'icu'
	'pcre2'
	'gnutls'
	'graphite'
	'lz4'
	'libgpg-error'
	'ffmpeg')
makedepends=(
	'git'
	'cmake'
	'extra-cmake-modules'
	'ninja')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(
	"git+${url}.git"
	"${_pkgname}-fix-qt6.8-build.patch::${url}/pull/36.patch"
)
sha256sums=('SKIP'
            '495a50534960a0f46b6e843543dd06d82f4c7514dd78122a074191f759ba6316')

pkgver() {
	cd Qcm
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd Qcm
	git submodule update --init --recursive
	patch -p1 <"../${_pkgname}-fix-qt6.8-build.patch"
	mkdir build
}

build() {
	cd Qcm/build
	cmake .. -GNinja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	ninja
}

package() {
	cd Qcm
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
