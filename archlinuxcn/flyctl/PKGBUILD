# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor: Trevor Mosey <trevor dot mosey at gmail dot com>

pkgname=flyctl
pkgver=0.2.100
pkgrel=1
pkgdesc="Command line tools for fly.io services"
arch=("x86_64")
url="https://github.com/superfly/${pkgname}"
license=("Apache-2.0")
makedepends=('go')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9b9afbf3911e266de73bb543154ee90e00f9f5de1121cf1fd8bd0f1ea8fd7140')

build() {
	cd "${pkgname}-${pkgver}/"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o bin/flyctl
}

check() {
	make -C "${pkgname}-${pkgver}" test
}

package() {
	install -Dm755 "${pkgname}-${pkgver}/bin/flyctl" -t "${pkgdir}/usr/bin/"
	ln -s "/usr/bin/${pkgname}" "${pkgdir}/usr/bin/fly"
}
