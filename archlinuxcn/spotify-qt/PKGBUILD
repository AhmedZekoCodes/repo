# Maintainer: Integral <integral@member.fsf.org>
# Contributor: kraxarn <kraxie@protonmail.com>

pkgname=spotify-qt
pkgver=3.12
pkgrel=2
pkgdesc="Lightweight Spotify client using Qt"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/kraxarn/${pkgname}"
license=("GPL-3.0-or-later")
depends=('qt6-base' 'qt6-svg' 'hicolor-icon-theme')
makedepends=('cmake')
optdepends=(
	"librespot: Recommended playback client"
	"spotifyd: Recommended playback client"
)
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
	"fix-qt6.9.patch::${url}/pull/284.patch"
)
sha256sums=('5782ff9cd854aa92b4ba49837d3ed4b5ada0427fd440e16866e9727abcd9df62'
            'bba5d74be875f79a99ba833f0de84be746c0d313e04b4c2e4f52b86636640c6c')

prepare() {
	patch -p1 -d "${pkgname}-${pkgver}" -i ../fix-qt6.9.patch
}

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
