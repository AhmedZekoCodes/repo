# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Pylogmon <pylogmon@outlook.com>

pkgname=clash-verge-rev
_pkgname=${pkgname%-rev}
pkgver=2.0.1
pkgrel=1
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri"
arch=('x86_64' 'i686' 'aarch64' 'armv7h')
url="https://github.com/${pkgname}/${pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk-4.1' 'gtk3' 'libayatana-appindicator')
conflicts=("${pkgname}-bin" "${pkgname}-alpha" "${pkgname}-alpha-bin" "${pkgname}-git" "${_pkgname}" "${_pkgname}-bin")
makedepends=('nodejs' 'pnpm' 'cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('d592e5646e6692623c16c015de2c98e206965073fadc8186514ea7dd84fecf55b6cf4461a8b3414c64c5b6b702b9414162943504813077d446051a92cdff528c')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	pnpm install
	pnpm check
}

build() {
	cd "${pkgname}-${pkgver}/"
	NODE_OPTIONS="--max_old_space_size=4096" pnpm build -b deb
}

package() {
	cd "${pkgname}-${pkgver}/"

	tar -xpf src-tauri/target/release/bundle/deb/Clash\ Verge_${pkgver}_*/data.tar.gz -C "${pkgdir}"
	chown -R root:root ${pkgdir}
}
