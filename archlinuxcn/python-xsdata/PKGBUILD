# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=xsdata
pkgname=python-${_name}
pkgver=25.4
pkgrel=1
pkgdesc="Naive XML & JSON Bindings for python"
arch=(any)
url="https://github.com/tefra/${_name}"
license=(MIT)
depends=(python python-typing_extensions python-click python-click-default-group python-docformatter python-jinja python-toposort ruff python-requests python-lxml)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-pytest-benchmark)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('9820b0594c0b8eb65894348176498cc11d052c2d8d9505187bbcbb176a8804d2df95c0e19b8241aa09a76be24420d37054c024f6b850927d484a5861b7877d95')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  cp -r docs "$pkgdir/usr/share/doc/$pkgname"
}
