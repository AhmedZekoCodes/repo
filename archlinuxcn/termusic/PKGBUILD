# Maintainer: orhun <orhunparmaksiz@gmail.com>
# https://github.com/orhun/pkgbuilds

pkgname=termusic
pkgver=0.9.0
pkgrel=1
pkgdesc="Music Player TUI written in Rust"
arch=('x86_64')
url="https://github.com/tramhao/${pkgname}"
license=('GPL-3.0-or-later AND MIT')
depends=('gstreamer' 'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad' 'gst-plugins-ugly' 'gst-libav' 'dbus' 'ueberzug' 'protobuf')
optdepends=('yt-dlp: download mp3'
	'ffmpeg: download mp3')
makedepends=('cargo' 'clang')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('59e81060852e6a215c24ce7faaf74c34f8d588895f0254a471a3bd895c68612d4e816f0f2a66cfd62885ae5706d27b11d34aa6a02c9d8620e2d0779a1edae8e6')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --features cover --release --frozen
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 target/release/${pkgname}{,-server} -t "${pkgdir}/usr/bin"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
	install -Dm644 LICENSE_{GPLv3,MIT} -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
