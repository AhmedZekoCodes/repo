# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=shh
pkgver=2025.2.7
pkgrel=1
pkgdesc="Automatic systemd service hardening guided by strace profiling"
arch=("x86_64")
url="https://github.com/desbma/shh"
license=("GPL-3.0-or-later")
depends=("gcc-libs" "glibc" "strace")
makedepends=("cargo")
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
b2sums=('fbb03e6425c8a6b74f92417a3da0e229c519fd648e44c2645e89943fceadc4bb7a730b3cf57896fad79d9dbbd43ed218265f525525f646d3574e0c4ad52f77dd')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n "s/host: //p")"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS='-C link-args=-Wl,-z,shstk'
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --bins
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
