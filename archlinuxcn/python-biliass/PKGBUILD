# Maintainer: Yufan You <ouuansteve at gmail>

pkgname=python-biliass
_pkgname=biliass
pkgver=1.3.13
pkgrel=1
pkgdesc='A tool to convert Bilibili XML/protobuf comments to ASS subtitles'
arch=('x86_64')
url='https://github.com/yutto-dev/yutto/tree/main/packages/biliass'
license=('GPL-3.0-or-later')
depends=(
    'gcc-libs'
    'glibc'
    'python'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-maturin'
    'python-wheel'
)
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('744d661df982453e21ee38036926c5bd7598079fa699030a48c7aefa8fc5a7e1')

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
