# Maintainer: bgme <i@bgme.me>
# Contributor: gilcu3
# Contributor: Y7n05h <Y7n05h@y7n05h.dev>

pkgname=ecapture
pkgver=0.9.3
pkgrel=3
pkgdesc="capture SSL/TLS text content without CA cert using eBPF"
arch=("x86_64" "aarch64")
url="https://github.com/gojue/ecapture"
license=("Apache-2.0")
provides=("ecapture")
conflicts=("ecapture-bin")
source=("${pkgname}::git+${url}#tag=v${pkgver}")
sha256sums=('66d306d20326b32854d883f91339408631709e1677d37305f1112556474bb913')

_linux_package_name=linux
depends=("glibc")
makedepends=("clang" "go" "git" "bpf" "${_linux_package_name}" "${_linux_package_name}-headers" "libelf" "llvm" "pkgconf")

build() {
    cd "${srcdir}/${pkgname}"
    UNAME_R=$(cat "/usr/src/${_linux_package_name}/version")
    HOST_VERSION_SHORT=$(echo "${UNAME_R}" | cut -d'-' -f 1)
    make "UNAME_R=${UNAME_R}" "HOST_VERSION_SHORT=${HOST_VERSION_SHORT}"
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "README.md" "README_CN.md" "README_JA.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
