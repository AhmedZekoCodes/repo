# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=eth-typing
pkgname=python-${_name}
pkgver=5.1.0
pkgrel=2
pkgdesc="Common type annotations for ethereum python packages"
arch=(any)
url="https://github.com/ethereum/${_name}"
license=(MIT)
depends=(python python-typing_extensions)
makedepends=(python-build python-installer python-setuptools python-wheel python-sphinx python-sphinx_rtd_theme)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('dff67584520549173a5e595df410e00fd61afc7284df5bcc32823b686e42600c3b1c992be6f6535165d0ef570494c6a368b6b948ea14dae9a2ddccbdef27f35a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  make -C docs man
}

check(){
  cd $_name-$pkgver    
  PYTHONPATH="$PWD:$PYTHONPATH" pytest tests/ -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 docs/_build/man/eth_typing.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
