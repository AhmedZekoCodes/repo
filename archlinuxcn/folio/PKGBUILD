# Maintainer: Kiri <kiri@vern.cc>

pkgname=folio
_name=Folio
pkgver=24.13
pkgrel=1
pkgdesc="A pretty note-taking app for GNOME | forked from Paper"
arch=('x86_64' 'aarch64')
url="https://github.com/toolstack/Folio"
license=('GPL-3.0-only')
depends=('libadwaita'
         'gtksourceview5'
         'libgee'
         'gtk4'
         'hicolor-icon-theme'
         'dconf'
         'pango'
         'glib2'
         'glibc'
         'sh')
makedepends=('meson'
             'ninja'
             'gobject-introspection'
             'blueprint-compiler'
             'python-gobject'
             'python-packaging'
             'vala'
             'glib2-devel')
checkdepends=('appstream-glib')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "folio.sh")
sha256sums=('164fbeb2b1263b6741ec88e63372d72b5c81d993e56d6ac808994c6b3be0c3f1'
            'bc7370d74d9581ef5301753a4ef72018c1e66af852bc5a9fc5fe602408705d6b')

build() {
  arch-meson "${_name}-${pkgver}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm 755 folio.sh ${pkgdir}/usr/bin/folio
}
