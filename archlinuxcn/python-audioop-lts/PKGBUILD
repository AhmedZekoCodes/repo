# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Vekhir <vekhir at yahoo dot com>

_pkgname=audioop
pkgname=python-audioop-lts
pkgver=0.2.1
pkgrel=2
pkgdesc="An LTS port of Python's 'audioop' module."
arch=('x86_64')
url="https://github.com/AbstractUmbra/audioop"
license=('PSF-2.0')
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
provides=(
  python-audioop
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AbstractUmbra/audioop/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2d6d6b5fe2aebb1d4dd87d090deb1a900b32bbc0b10d6b441bc41bc792e5bc0d0c6d460dbe9ed2764e49c577c1fcdaf0cf9a785af6126a9b0d3b1ad6a467ea3f')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pkgname}-${pkgver}"
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" \
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}/" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
