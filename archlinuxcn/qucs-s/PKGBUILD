# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Zes4Null <rjw1248[at]hotmail[dot]com>
# Contributor: Philip <philip1731 protonmail ch>

pkgname=qucs-s
pkgver=24.4.0
pkgrel=2
pkgdesc="A spin-off of Qucs that supports other free SPICE circuit simulators like ngspice with the same Qucs GUI"
arch=('x86_64' 'i686')
url="https://ra3xdh.github.io"
license=('GPL-2.0-or-later')
depends=(
    'glibc'
    'gcc-libs'
    'sh'
    'qt6-base'
    'qt6-svg'
    'qt6-charts'
    'ngspice'
    'python'
    'python-numpy'
    'python-matplotlib'
    'hicolor-icon-theme'
)
makedepends=(
    'cmake'
    'qt6-tools'
    'ninja'
    'gperf'
    'dos2unix'
)
optdepends=(
    'qucs: Qucsator simulation backend'
    'xyce-serial: SPICE-compatible simulation backend'
    'spiceopus: general purpose simulation backend for optimization loops'
    'openvaf: compiler for Verilog-A devices support'
    'octave: high-level language for post-simulation data processing'
)
source=(https://github.com/ra3xdh/qucs_s/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('0af7604394fa5112c8e3e8c3f81afccabe3a91a8d455286e073dbc4f3677879e86451ae7ff05d83f225058b00bb1401026c36599f67ac7b8be69bf2926bf7b63')

build() {
    cmake -S "$pkgname-$pkgver" -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT6=ON
        
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}

# vim: set ts=4 sw=4 et:
