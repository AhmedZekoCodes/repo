# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=scenedetect
pkgname=python-scenedetect
pkgver=0.6.5.2
pkgrel=1
pkgdesc='Python and OpenCV-based scene cut/transition detection program & library'
arch=('any')
url='http://scenedetect.com'
license=('BSD-3-Clause')
depends=(
  python-click
  python-numpy
  python-opencv
  python-platformdirs
  python-tqdm
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
optdepends=(
  "ffmpeg: required to split video files"
  "mkvtoolnix-cli: required to split video files in stream copy mode"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('a0ff519f23352cdacdd83ed8841a288a2364ccd7aeebc9594294d6fb503f3545297566f6f04a7e1f71535753f802ae899e58f2619f909a85e38413d061f27a1c')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
